import QtQuick 2.6
import Sailfish.Silica 1.0
import Aurora.Controls 1.0
import io.thp.pyotherside 1.5


Page {
    id: page

    readonly property bool multiPagePdfCreation: idComboBoxFileExtension.currentIndex === 5
    readonly property bool canSave: !processing && multiPagePdfCreation === pageNumberMultiPDF > 0

    // values transmitted from FirstPage.qml
    property var homeDirectory
    property var origImageFileName
    property var origImageFolderPath
    property var tempImageFolderPath
    property var imageWidthSave
    property var imageHeightSave
    property var inputPathPy

    // variables for saving
    property var oldFileType
    property var oldFileName
    property var origImageFileNameArray
    property var savePath
    property var estimatedFileSize
    property var pageNumberMultiPDF : 0
    property var multiPdfPageNamesList : ""
    property bool validatorNameOverwrite : false

    // variables for warning overwrite
    property var estimatedFolder

    property bool processing

    allowedOrientations: Orientation.All

    Component.onCompleted: {
        // get infos from the original file
        origImageFileNameArray = origImageFileName.split(".")
        oldFileName = (origImageFileNameArray.slice(0, origImageFileNameArray.length-1)).join(".")
        oldFileType = origImageFileNameArray[origImageFileNameArray.length - 1]

        if (oldFileType.indexOf('jpg') !== -1 || oldFileType.indexOf('jpeg') !== -1) {
            idComboBoxFileExtension.currentIndex = 0
        } else if (oldFileType.indexOf('png') !== -1) {
            idComboBoxFileExtension.currentIndex = 1
        } else if (oldFileType.indexOf('gif') !== -1) {
            idComboBoxFileExtension.currentIndex = 2
        } else if (oldFileType.indexOf('bmp') !== -1) {
            idComboBoxFileExtension.currentIndex = 3
        } else {
            // suggested file format if none of the above
            idComboBoxFileExtension.currentIndex = 0
        }

        py.getImageSizeFunction()
        py.getMultiPdfPagesFunction()
    }

    Python {
        id: py

        Component.onCompleted: {
            addImportPath(Qt.resolvedUrl('../py'))
            importModule('graphx', function () {})

            setHandler('tempFilesDeleted', function(i) { console.log("temp files deleted: " + i) })

            setHandler('estimatedFileSize', function(estimatedSize) {
                estimatedFileSize = Math.round(parseInt(estimatedSize) / 10) / 100
            })

            setHandler('fileIsSaved', function() { pageStack.pop() })
            setHandler('fileMultiPagePdfIsAdded', function() { page.processing = false })

            setHandler('getPagesMultiPDF', function(pagesCounter, pagesNamesList) {
                pageNumberMultiPDF = parseInt(pagesCounter)
                multiPdfPageNamesList = pagesNamesList
            })

            setHandler('tempMultiPDFfilesDeleted', function() { page.processing = false })
            setHandler('debugPythonLogs', function(i) { console.log(i) })
        }

        // file operations
        
        function saveFunction() {
            page.processing = true

            var folderSavePath
            if (idComboBoxTargetFolder.currentIndex === 0) {
                if (page.multiPagePdfCreation || idComboBoxFileExtension.currentIndex === 4) {
                    folderSavePath = homeDirectory + "/Documents/"
                } else {
                    folderSavePath = origImageFolderPath
                }
            } else if (idComboBoxTargetFolder.currentIndex === 1) {
                folderSavePath = homeDirectory + "/Pictures" + "/Imageworks/"
            } else if (idComboBoxTargetFolder.currentIndex === 2) {
                folderSavePath = homeDirectory + "/Pictures/"
            } else if (idComboBoxTargetFolder.currentIndex === 3) {
                folderSavePath = homeDirectory + "/Downloads/"
            } else if (idComboBoxTargetFolder.currentIndex === 4) {
                folderSavePath = homeDirectory + "/"
            }

            savePath = folderSavePath + idFilenameNew.text.toString() + idComboBoxFileExtension.value.toString()
            inputPathPy = ( "/" + inputPathPy.replace(/^(file:\/{3})|(qrc:\/{2})|(http:\/{2})/,"") )
            var fileTargetType = idComboBoxFileExtension.value.toString()
            var pdfResolution = 300

            const args = [ inputPathPy, savePath, tempImageFolderPath, pdfResolution, fileTargetType ]
            call("graphx.saveNowFunction", args)
        }

        function getImageSizeFunction() {
            inputPathPy = decodeURIComponent("/" + inputPathPy.replace(/^(file:\/{3})|(qrc:\/{2})|(http:\/{2})/,""))
            call("graphx.getImageSizeFunction", [ inputPathPy ])
        }

        function gatherMultiPagePdfFunction() {
            page.processing = true
            pageNumberMultiPDF = pageNumberMultiPDF + 1

            multiPdfPageNamesList = multiPdfPageNamesList + pageNumberMultiPDF 
                                    + "-" + idFilenameNew.text.toString() + "\n"

            inputPathPy = ( inputPathPy.replace(/^(file:\/{3})|(qrc:\/{2})|(http:\/{2})/,"") )

            call("graphx.gatherMultiPagePdfFunction", [ inputPathPy, pageNumberMultiPDF, multiPdfPageNamesList ])
        }

        function getMultiPdfPagesFunction() {
            call("graphx.getMultiPdfPagesFunction", [])
        }

        function deleteTempMultiPagePDF() {
            call("graphx.deleteTempMultiPagePDF", [ tempImageFolderPath ])
        }

        function createMultiPagePDFFunction() {
            var folderSavePath
            if (idComboBoxTargetFolder.currentIndex === 0) {
                if (page.multiPagePdfCreation || idComboBoxFileExtension.currentIndex === 4) {
                    folderSavePath = homeDirectory + "/Documents/"
                } else {
                    folderSavePath = origImageFolderPath
                }
            } else if (idComboBoxTargetFolder.currentIndex === 1) {
                folderSavePath = homeDirectory + "/Pictures" + "/Imageworks/"
            } else if (idComboBoxTargetFolder.currentIndex === 2) {
                folderSavePath = homeDirectory + "/Pictures/"
            } else if (idComboBoxTargetFolder.currentIndex === 3) {
                folderSavePath = homeDirectory + "/Downloads/"
            } else if (idComboBoxTargetFolder.currentIndex === 4) {
                folderSavePath = homeDirectory + "/"
            }

            savePath = folderSavePath + idFilenameMultiPDF.text.toString() + ".pdf"
            call("graphx.createMultiPagePDFFunction", [ savePath, tempImageFolderPath ])
        }

        onError: console.log('python error: ' + traceback);
        onReceived: console.log('got message from python: ' + data);
    } // end Python

    AppBar {
        id: appBar

        headerText: qsTr("Save as...")

        AppBarSpacer { }

        AppBarButton {
            icon.source: "image://theme/icon-splus-accept"
            enabled: page.canSave

            onClicked: {
                if (page.multiPagePdfCreation) {
                    py.createMultiPagePDFFunction()
                } else {
                    py.saveFunction()
                }
            }
        }
    }

    BusyLabel {
        running: page.processing
        text: qsTr("Applying changes")
    }

    SilicaFlickable {
        id: listView
        
        anchors {
            fill: parent
            topMargin: appBar.height
        }

        contentHeight: columnSaveAs.height
        opacity: page.processing ? 0 : 1
        visible: opacity > 0

        Behavior on opacity {
            FadeAnimation { }
        }
        
        VerticalScrollDecorator { }

        Column {
            id: columnSaveAs

            width: page.width

            Row {
                width: parent.width

                TextField {
                    id: idFilenameNew

                    label: validatorNameOverwrite ? qsTr("overwrite...") : ""
                    enabled: !page.multiPagePdfCreation
                    width: parent.width - idComboBoxFileExtension.width
                    anchors.top: parent.top
                    anchors.topMargin: Theme.paddingMedium
                    y: Theme.paddingSmall
                    inputMethodHints: Qt.ImhNoPredictiveText
                    text: oldFileName + "_edit"

                    validator: RegExpValidator { 
                        // negative list
                        regExp: /^[^<>'\"/;*:`#?]*$/ 
                    }
                    
                    EnterKey.onClicked: idFilenameNew.focus = false

                    onTextChanged: checkOverwriting()
                }

                ComboBox {
                    id: idComboBoxFileExtension

                    width: {
                        var maxWidth = 0

                        for (var i = 0; i < menu.children.length; i++) {
                            maxWidth = Math.max(maxWidth, extensionMetrics.boundingRect(menu.children[i].text).width)
                        }

                        return maxWidth + Theme.paddingLarge * 4
                    }
                    
                    menu: ContextMenu {
                        MenuItem {
                            text: ".jpg"
                            font.pixelSize: Theme.fontSizeExtraSmall
                        }
                        MenuItem {
                            text: ".png"
                            font.pixelSize: Theme.fontSizeExtraSmall
                        }
                        MenuItem {
                            text: ".gif"
                            font.pixelSize: Theme.fontSizeExtraSmall
                        }
                        MenuItem {
                            text: ".bmp"
                            font.pixelSize: Theme.fontSizeExtraSmall
                        }
                        MenuItem {
                            text: ".pdf"
                            font.pixelSize: Theme.fontSizeExtraSmall
                        }
                        MenuItem {
                            text: "PDF+"
                            font.pixelSize: Theme.fontSizeExtraSmall
                        }
                    }

                    FontMetrics {
                        id: extensionMetrics
                    }
                }
            } // end row save filename

            Row {
                visible: page.multiPagePdfCreation
                width: parent.width
                
                TextField {
                    id: idFilenameMultiPDF
                    
                    enabled: pageNumberMultiPDF > 0
                    width: parent.width - idComboBoxFileExtensionMultiPDF.width
                    anchors.top: parent.top
                    anchors.topMargin: Theme.paddingMedium
                    y: Theme.paddingSmall
                    inputMethodHints: Qt.ImhNoPredictiveText
                    text: "multipage"
                    
                    validator: RegExpValidator { 
                        regExp: /[a-zA-Z0-9äöüÄÖÜ_=()\/.!?#+-]*$/ 
                    }
                    
                    EnterKey.onClicked: idFilenameNew.focus = false
                }

                ComboBox {
                    id: idComboBoxFileExtensionMultiPDF
                    
                    enabled: false
                    width: idComboBoxFileExtension.width

                    menu: ContextMenu {
                        MenuItem {
                            text: ".pdf"
                            font.pixelSize: Theme.fontSizeExtraSmall
                        }
                    }
                }
            } // end row save filename

            ComboBox {
                id: idComboBoxTargetFolder
                
                width: parent.width
                label: qsTr("Save location")
                
                menu: ContextMenu {
                    id: idCropShape
                    
                    MenuItem {
                        text: page.multiPagePdfCreation || idComboBoxFileExtension.currentIndex === 4 
                              ? qsTr("Documents") 
                              : qsTr("Original Folder")
                        font.pixelSize: Theme.fontSizeExtraSmall
                    }
                    MenuItem {
                        text: "Pictures/Imageworks"
                        font.pixelSize: Theme.fontSizeExtraSmall
                    }
                    MenuItem {
                        text: "Pictures"
                        font.pixelSize: Theme.fontSizeExtraSmall
                    }
                    MenuItem {
                        text: "Downloads"
                        font.pixelSize: Theme.fontSizeExtraSmall
                    }
                    MenuItem {
                        text: "/home"
                        font.pixelSize: Theme.fontSizeExtraSmall
                    }
                }

                onCurrentItemChanged: checkOverwriting()
            }

            SectionHeader {
                visible: page.multiPagePdfCreation
                text: "\n" + qsTr("Pages Contained")
                horizontalAlignment: Text.AlignLeft
            }

            Item {
                width: 1
                height: Theme.paddingMedium
            }

            ButtonLayout {
                visible: page.multiPagePdfCreation

                Button {
                    icon.source: "image://theme/icon-splus-add"
                    text: qsTr("Add page")

                    onClicked: py.gatherMultiPagePdfFunction()
                }

                Button {
                    icon.source: "image://theme/icon-splus-clear"
                    text: qsTr("Clear")
                    enabled: page.pageNumberMultiPDF > 0

                    onClicked: py.deleteTempMultiPagePDF()
                }
            }


            Item {
                width: 1
                height: Theme.paddingMedium
                visible: page.multiPagePdfCreation
            }

            Label {
                anchors {
                    left: parent.left
                    right: parent.right
                    leftMargin: Theme.horizontalPageMargin
                    rightMargin: anchors.leftMargin
                }

                visible: page.multiPagePdfCreation
                font.pixelSize: Theme.fontSizeExtraSmall
                text: multiPdfPageNamesList
                wrapMode: Text.WordWrap
            }
            
            Label {
                anchors {
                    left: parent.left
                    right: parent.right
                    leftMargin: Theme.horizontalPageMargin
                    rightMargin: anchors.leftMargin
                }

                visible: !page.multiPagePdfCreation
                font.pixelSize: Theme.fontSizeExtraSmall
                wrapMode: Text.WordWrap
                
                text: qsTr("Original Folder") + ": " + origImageFolderPath + "\n"
                        + qsTr("Width") + ": " + imageWidthSave + "\n"
                        + qsTr("Height") + ": " + imageHeightSave + "\n"
                        + qsTr("Size") + ": " + estimatedFileSize + " kb"
            }

            Label {
                id: idWarningTransparencySupport
                
                visible: idComboBoxFileExtension.currentIndex !== 1 && !page.multiPagePdfCreation
                topPadding: Theme.iconSizeExtraSmall
                leftPadding: Theme.paddingLarge * 1.2
                width: parent.width - 2*Theme.paddingLarge
                font.pixelSize: Theme.fontSizeExtraSmall
                text: qsTr("Filetype does not support transparency.")
            }

        } // end Column
    } // end Silica Flickable

    function checkOverwriting() {
        if (idComboBoxTargetFolder.currentIndex === 0) {
            if (page.multiPagePdfCreation || idComboBoxFileExtension.currentIndex === 4) {
                estimatedFolder = homeDirectory + "/Documents/"
            } else {
                estimatedFolder = origImageFolderPath
            }
        } else if (idComboBoxTargetFolder.currentIndex === 1) {
            estimatedFolder = homeDirectory + "/Pictures" + "/Imageworks/"
        } else if (idComboBoxTargetFolder.currentIndex === 2) {
            estimatedFolder = homeDirectory + "/Pictures/"
        } else if (idComboBoxTargetFolder.currentIndex === 3) {
            estimatedFolder = homeDirectory + "/Downloads/"
        } else if (idComboBoxTargetFolder.currentIndex === 4) {
            estimatedFolder = homeDirectory + "/"
        }

        validatorNameOverwrite = estimatedFolder === origImageFolderPath 
                                 && oldFileName === idFilenameNew.text 
                                 && "." + oldFileType === idComboBoxFileExtension.value.toString()
    }
}
